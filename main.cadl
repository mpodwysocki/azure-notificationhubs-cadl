import "@cadl-lang/rest";
import "./models/installation.cadl";
import "./models/job.cadl";
import "./models/notification.cadl";
import "./models/registration.cadl";

using Cadl.Http;
using NotificationHubs.Models;

@serviceTitle("Azure Notification Hubs Service")
@serviceVersion("2020-06")
@server("https://{namespaceName}.servicebus.windows.net/{hubName}", "Notification Hubs Namespace", {
  @doc("Notification Hubs Namespace")
  namespaceName?: string,
  @doc("Notification Hub Name")
  hubName?: string,
})
@doc("This is the entry point for an Azure Notification Hub")
namespace NotificationHubs;

@route("/installations")
namespace Installations {
  @get
  op get(@path installationId: string): InstallationCommon;
}

@route("/registrations")
namespace Registrations {
  @put
  op create(
    @path registrationId: string,
    @header contentType: "application/atom+xml;type=entry;charset=utf-8"
  ): {
    @body body: RegistrationDescription
  };

  @get
  op get(@path registrationId: string): {
    @body body: RegistrationDescription
  };
}

@route("/messages")
namespace Notifications {
  @post
  op sendNotification(
    @query direct?: boolean,
    @body notification: Notification,
    @header("ServiceBusNotification-Format") platform: string,
    @header test: boolean,
    @header("ServiceBusNotification-Tags") tags: string,
  ): {
    @header location: string;
    @header trackingId: string;
    @header("x-ms-correlation-request-id") correlationId: string;
  };
}
